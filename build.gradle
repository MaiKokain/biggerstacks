buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'eclipse'
    id 'maven-publish'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

version = "$shortMinecraftVersion-$modVersion"
group = 'PORTB'
archivesBaseName = 'biggerstacks'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

minecraft {
    mappings channel: 'official', version: minecraftVersion

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'biggerstacks'

            args(["--username", "Dev###"])

            mods {
                biggerstacks {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('serverrun')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'biggerstacks'

            mods {
                biggerstacks {
                    source sourceSets.main
                }
            }
        }

        gameTestServer {
            workingDirectory project.file('gametestrun')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'biggerstacks'

            mods {
                biggerstacks {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('datarun')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'biggerstacks', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                biggerstacks {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "biggerstacks.refmap.json"

    config "biggerstacks.mixins.json"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
    mavenLocal()

    maven { // IE & CT
        name = "BlameJared"
        url = "https://maven.blamejared.com/"
    }
    maven { // JEI
        name 'DVS1 Maven FS'
        url 'https://dvs1.progwml6.com/files/maven'
    }
    maven { //Applied energy sticks 2
        url = uri("https://maven.pkg.github.com/AppliedEnergistics/Applied-Energistics-2")
        credentials {
            username = "github_is_stupid"
            password = "\u0067hp_oGjcD" /*im blue daba de daba di*/ + "FCn8jeTzIj4Ke9pLoEVtpnZMP4VQgaX"
        }
    }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:$minecraftVersion-$forgeVersion"

    def cm = "curse.maven"

    //
    // Compile only dependencies
    // Non-deobf jars
    //
    [
            "appeng:appliedenergistics2:8.4.7",
            "blusunrize.immersiveengineering:ImmersiveEngineering:1.16.5-5.1.0-148",
            "$cm:RefinedStorage-243076:3807951",         //https://www.curseforge.com/minecraft/mc-mods/refined-storage/files/
            "$cm:Mekanism-268560:3659389",               //https://www.curseforge.com/minecraft/mc-mods/mekanism/files/
            "$cm:ShetiPhianCore-71738:3832649",          //https://www.curseforge.com/minecraft/mc-mods/shetiphiancore/files/
            "$cm:IndustrialForegoing-266515:3446262",    //https://www.curseforge.com/minecraft/mc-mods/industrial-foregoing/files/
            "$cm:Titanium-287342:3346366",               //https://www.curseforge.com/minecraft/mc-mods/titanium/files/
    ].forEach { dep -> compileOnly fg.deobf(dep) }

    //
    // Compile only dependencies
    // Deobf jars only
    //
    [
            "$cm:CyclopsCore-232758:3796010",            //https://www.curseforge.com/minecraft/mc-mods/cyclops-core/files
            "$cm:ColossalChests-237875:3474924",         //https://www.curseforge.com/minecraft/mc-mods/colossal-chests/files
            //maven local repos
            "com.lothrazar.cyclic:Cyclic:1.16.5-1.5.17:deobf",
            "vazkii.quark:Quark:r2.4-323:deobf",
            "com.github.mcjty:rftoolsbase:1.16-2.1.5:deobf",
            "com.github.mcjty:rftoolsstorage:1.16-2.0.19:deobf",
            "com.github.mcjty:mcjtylib:1.16-5.1.8:deobf",
            "com.github.mcjty:xnet:1.16-3.0.17:deobf",
            "com.github.mcjty:rftoolsutility:1.16-3.1.16:deobf",
            "me.desht:modular-routers:1.16.5-7.5.3-CUSTOM:deobf",
            "edivad.dimstorage:DimStorage-1.16.5:4.4.1:deobf",
            "de.ellpeck.prettypipes:PrettyPipes:1.9.8:deobf",
            "slimeknights.tconstruct:TConstruct:1.16.5-3.3.4.DEV.5424fac0a:deobf",
            "slimeknights.mantle:Mantle:1.16.5-1.6.DEV.e3377ec:deobf",
            "sophisticatedbackpacks:sophisticatedbackpacks:1.16.5-3.15.15.-alpha:deobf",
            "com.almostreliable.lazierae2:lazierae2:1.16.5-2.0.4",
            "org.squiddev:cc-tweaked-1.16.5:1.100.8:deobf",
    ].forEach { dep -> compileOnly(dep) }

    //
    // Implementation dependencies
    // Used for testing
    //
    //[
    //].forEach { dep -> implementation fg.deobf(dep) }

    //
    // Runtime only dependencies
    //
    [
            "com.blamejared.crafttweaker:CraftTweaker-1.16.5:7.1.2.511", //used for testing blacklist/whitelist tag
            "mezz.jei:jei-1.16.5:7.7.1.152",
            //"$cm:ConfigMenu-544048:3520850",             //https://www.curseforge.com/minecraft/mc-mods/config-menus-forge/files/
            //"$cm:BetterModsButton-541584:3519038",       //https://www.curseforge.com/minecraft/mc-mods/better-mods-button/files/
    ].forEach { dep -> runtimeOnly fg.deobf(dep) }

    //
    // Annotation processor
    //
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "biggerstacks",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion.get(),
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
