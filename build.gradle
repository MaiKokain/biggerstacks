import portb.submoduleplugin.SubmodulesPlugin

buildscript {
    ext.kotlin_version = '1.9.22'
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'portb.gradle.ModSubmoduleBuildFromSource' version "1.2.2"
    id "com.modrinth.minotaur" version "2.+"
}

apply plugin: SubmodulesPlugin
apply plugin: 'kotlin'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'org.spongepowered.mixin'

version = "$minecraftVersion-$modVersion"
group = 'portb'
archivesBaseName = 'biggerstacks'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

minecraft {
    mappings channel: 'official', version: minecraftVersion
    //no parchment for 1.19.4
    //mappings channel: 'parchment', version: "$parchmentVersion-$minecraftVersion"

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'biggerstacks'

            args(["--username", "Dev###"])

            mods {

                biggerstacks {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('serverrun')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'biggerstacks'

            mods {
                biggerstacks {
                    source sourceSets.main
                }
            }
        }

        gameTestServer {
            workingDirectory project.file('gametestrun')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'biggerstacks'

            mods {
                biggerstacks {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('datarun')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'biggerstacks', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                biggerstacks {
                    source sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "biggerstacks.refmap.json"

    config "biggerstacks.mixins.json"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
    mavenLocal()

    maven { // JEI
        name 'DVS1 Maven FS'
        url 'https://dvs1.progwml6.com/files/maven'
    }
    maven {
        url = "https://codeberg.org/api/packages/PORTB/maven"
    }
}

//
// Submodule config
//
submodules {
    submodulesDirectory = file("mod_subs")

    repos {
        refinedstorage {
            enableDeobf = false
            tasks = ["build"]
        }
        ae2 {
            tasks = ["runData", "build"]
            jarTask = "shadowJar"
            enableDeobf = false
            customGradleCode = """
            afterEvaluate {
                tasks.spotlessJava.enabled = false
                tasks.spotlessJavaCheck.enabled = false
                tasks.spotlessJson.enabled = false
            }
            """.stripIndent()

            ignoreErrors = true
        }
        mekanism {
            tasks = ["build"]
        }
        xnet {
            tasks = ["build"]
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:$minecraftVersion-$forgeVersion"

    //
    // Embedded libraries
    //
    jarJar.enable()
    project.configurations.minecraftLibrary.extendsFrom(project.configurations.jarJar)

    jarJar "portb:BiggerStacksConfigLib:$configLibVersion",
            "portb:BiggerStacksTransformerLib:$transformerLibVersion",
            "portb:SimpleLoggerWrapper:$simpleLoggerWrapperVersion"

    jarJar("com.thoughtworks.xstream:xstream:[1.4.20]") {
        //we do not want xstream's built in parser. We will use stax.
        exclude group: 'io.github.x-stream', module: 'mxparser'
    }

    //
    // Mixin
    //
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

//    runtimeOnly fg.deobf("portb:unifiedoptions:1.20.1-1.1")
}

jar {
    manifest {
        attributes([
                "Specification-Title"        : "biggerstacks",
                "Implementation-Title"       : project.name,
                "Implementation-Version"     : project.jar.archiveVersion.get(),
                "Implementation-Timestamp"   : new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "TransformerLib-Version"     : transformerLibVersion,
                "ConfigLib-Version"          : configLibVersion,
                "SimpleLoggerWrapper-Version": simpleLoggerWrapperVersion
        ])
    }
}

jar.finalizedBy('reobfJar')

reobf {
    jarJar {}
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}